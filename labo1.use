-- TP1 - USE
-- Auteur: Daniel Desroches
-- Date: 3 fev 2015
--
-- Fichier: labo1.use
--
-- Description:
--

--

model convois

-- ============
-- Enumerations
-- ============

-- definis les types de temps possible
enum WeatherType {cloudy, raining, snowing, sunny}

-- =======
-- classes
-- =======

-- ensemble de voies exclusivement reservee a la
-- circulation des vehicules
class Highway
attributes
  Length : Integer
  MaxSpeed : Integer
  MinSpeed : Integer
operations
  everyVehiclesRespectSpeedLimit() : Boolean
  post ReturnValue : self.lanes->forAll(l | 
    l.objects->forAll(o | 
      if o.oclIsTypeOf(Vehicle) then
        result = o.oclAsType(Vehicle).Speed <= MaxSpeed and o.oclAsType(Vehicle).Speed >= MinSpeed 
      else
        result = true
      endif)
      )
end

-- voie exclusivement reservee a la circulation
-- des vehicules
class Lane
attributes
end

-- obstacle se trouvant sur une voie pour obstruer
-- la voie des vehicules
class Object
attributes
  Position : Integer
end

-- lieu ou se dirige un vehicule
class Destination
attributes
  Position : Integer
  hasGaz : Boolean
end

-- s'occupe de la reparation et entretien d'un vehicule
class Garage < Destination
attributes
operations
  checkRepairBrand(vehicle : Vehicle) : Boolean
  post ReturnValue : result = self.brands->exists(b | b.Name = vehicle.brand.Name)
  
  repairVehicle(vehicle : Vehicle)
  begin
    vehicle.Navi.checkengine.isOk := true
  end
  pre vehicleAtPosition: vehicle.Position = self.Position
  pre vehicleNeedsRepair: vehicle.Navi.checkengine.isOk = false
  pre GarageCanRepair: self.brands->exists(b | b.Name = vehicle.brand.Name)
  post repaired: vehicle.Navi.checkengine.isOk = true
end

-- moyen de transport motorise

-- startConvoy
-- permet de demarrer un convoi de voiture
class Vehicle < Object
attributes
  Speed : Integer
  FuelLevel : Real
  Following : Vehicle
  FollowedBy : Vehicle
  Navi : Navigator
operations
  startConvoy(vehicle:Vehicle)
  begin
    self.FollowedBy := vehicle;
    vehicle.Following := self;
    vehicle.Navi.isAlive := true;
  end
  pre VehicleStartingNotFollowing: self.Following.isUndefined
  pre VehicleStartingNotFollowed: self.FollowedBy.isUndefined
  pre VehicleStartingNoNavigator: self.Navi.isAlive = false
  pre VehicleEntryNotFollowing: vehicle.Following.isUndefined
  pre VehicleEntryNoNavigator: vehicle.Navi.isAlive = false
  post VehicleStartedNotFollowing: self.Following.isUndefined
  post VehicleStartedFollowed: self.FollowedBy = vehicle
  post VehicleStartedNoNavigator: self.Navi.isAlive = false
  post VehicleEnteredFollowing: vehicle.Following = self
  post VehicleEnteredNavigatorOn: vehicle.Navi.isAlive=true
  
  joinConvoy(vehicle:Vehicle)
  begin
    self.Following := vehicle;
    vehicle.FollowedBy := self;
    self.Navi.isAlive := true;
  end
  pre FrontCarFollowingSomeone: not (vehicle.Following.isUndefined)
  pre FrontCarFollowedByNobody: vehicle.FollowedBy.isUndefined
  pre FrontCarNaviOn: vehicle.Navi.isAlive=true
  pre VehicleEntryFollowingNoOne: self.Following.isUndefined
  pre VehicleEntryNoNavigator: vehicle.Navi.isAlive = false
  pre FrontCarStillFollowingSomeone: not (vehicle.Following.isUndefined)
  pre FrontCarNowFollowedBySelf: vehicle.FollowedBy = self
  pre FrontCarNaviStillOn: vehicle.Navi.isAlive=true
  pre VehicleEntryNowFollowingSomeone: self.Following = vehicle
  pre VehicleEntryNowNavigatorOn: vehicle.Navi.isAlive = true
    
  
  leaveConvoy()
  begin
    self.Following.FollowedBy := null;
    self.Following := null;
    self.Navi.isAlive := false;
  end
  pre VehicleIsFollowing: self.Following.isUndefined = false
  pre VehicleHeadFollowed: self.Following.FollowedBy = self
  pre VehicleStartedNoNavigator: self.Navi.isAlive = true
  post VehicleHeadNotLongerFollowed: not Vehicle.allInstances->exists(v | v.Following = self)
  post VehicleLeavingNoNavigator: self.Navi.isAlive = false
  post VehicleLeavingNotFollowing: self.FollowedBy.isUndefined
  
  respectSpeedLimit (speed : Integer) : Boolean
  post ReturnValue : if self.Speed = speed then
    result = true
  else
    result = false
  endif
  
  vehicleNeedRepair() : Boolean
  post ReturnValue : result = (self.Navi.checkengine.isOk = false)
  
  changeLane(lane : Lane)
  pre LaneNotCurrentLane: not (self.lane = lane)
  post LaneNowCurrentLane: self.lane = lane
  post LaneNotOldLane: not (self.lane = self.lane@pre)
  
  setDestination(dest : Destination)
  pre DestinationNotCurrentDestination: not (self.Navi.destination = dest)
  pre NavigatorOn: self.Navi.isAlive = true
  post DestinationNowCurrentDestination: self.Navi.destination = dest
  post DestinationNowOldDestination: not (self.Navi.destination = self.Navi.destination@pre)
  post NavigatorStillOn: self.Navi.isAlive = true
end

-- une facon unique d'identifier un vehicule a cause
-- de son style personnel
class Brand
attributes
  Length : Integer
  Name : String
end

-- permet a l'utilisateur de ce faire guider automatiquement
-- vers sa destination
class Navigator
attributes
  CruiseSpeed : Integer
  isAlive : Boolean
end
 
-- etat du temps exterieur qui permet de definir
-- la distance de securite que les vehicules
-- doivent prendre
class GlobalWeather
attributes
  SafeDistanceRatio : Real
operations
  calculateSafeDistance (weatherType:WeatherType) : Real
    post ReturnValue : if weatherType=#cloudy or weatherType=#sunny then
      result = 1.0
    else
      if weatherType=#raining then
        result = 0.9
      else
        result = 0.75
      endif
    endif
end

-- detecteur qui dit au conducteur du vehicule
-- si son moteur a un probleme et il doit arreter
-- dans un garage
class CheckEngine
attributes
  isOk : Boolean
end

-- ==========
-- Contraints
-- ==========

constraints

-- en aucun temps, un objet dans une ligne peut avoir la même position 
-- qu'un autre objet.
context Lane inv NoObjectsCollide:
  self.objects.Position->asSet()->size() = self.objects.Position->asBag()->size()

-- Un véhicule ne peut aller plus rapidement que la limite de vitesse
-- de l'autoroute le permet.
context Vehicle inv RespectMaxSpeed:
  self.Speed <= self.lane.highway.MaxSpeed

-- Un véhicule qui a un autre véhicule qui le suit, se doit d'avoir
-- lui-même en tant que véhicule de tête pour le véhicule qui le suit.
context Vehicle inv FollowedByTheFollowing:
  self.frontcar.isUndefined=false implies self.frontcar.following = self

-- En aucun temps, deux véhicules ne peuvent être suivi par le même véhicule.
context Vehicle inv NotFollowedByTheSameVehicle:
  Vehicle.allInstances->collect(v | v.frontcar)->size() = Vehicle.allInstances->collect(v | v.frontcar)->asSet()->size() 
  
  
-- En aucun temps, deux véhicules ne peuvent suivre le même véhicule.
context Vehicle inv NotFollowingTheSameVehicle:
  Vehicle.allInstances->collect(v | v.following)->size() = Vehicle.allInstances->collect(v | v.following)->asSet()->size() 
  
-- Un véhicule qui suit un autre véhicule, se doit d'avoir
-- lui-même en tant que véhicule suivant le véhicule de tête.
context Vehicle inv FollowingTheFollowedBy:
  self.following.isUndefined=false implies self.following.frontcar = self
  
-- Un véhicule suivant un autre doit avoir son navigateur actif.
context Vehicle inv NaviOnWhileFollowing:
  self.following.isUndefined=false implies self.navigator.isAlive=true
  
-- ============
-- Associations
-- ============

association LanesLink between
  Highway[1] role highway
  Lane[1..*] role lanes
end

association ObjectsLink between
  Lane[1] role lane
  Object[*] role objects
end

association DestinationsLink between
  Lane[1] role lane
  Destination[*] role destinations
end

association canRepair between
  Garage[1] role mechanic
  Brand[1..*] role brands
end

association FollowedByLink between
  Vehicle[0..1] role head
  Vehicle[0..1] role followed
end

association FollowingLink between
  Vehicle[0..1] role following
  Vehicle[0..1] role frontcar
end

association NavigatorLink between
  Vehicle[1] role installation
  Navigator[1] role navigator
end

association BrandLink between
  Vehicle[1] role vehicle
  Brand[1] role brand
end

association DestinationLink between
  Navigator[1] role mobile
  Destination[1] role destination
end

association CheckEngineLink between
  Navigator[1] role commander
  CheckEngine[1] role checkengine
end

association GlobalWeatherLink between
  Navigator[1] role commander
  GlobalWeather[1] role weathersystem
end
